terraform {
  required_providers {
    confluent = {
      source  = "confluentinc/confluent"
      version = "1.46.0"
    }
  }
}

provider "confluent" {
  cloud_api_key    = var.cloud_api_key
  cloud_api_secret = var.cloud_api_secret
}

data "confluent_kafka_cluster" "main" {
  id = var.kafka_id
  environment {
    id = var.environment_id
  }
}

// 'app-manager' service account is required in this configuration to create var.topic_name topic and grant ACLs
// to 'app-producer' and 'app-consumer' service accounts.
resource "confluent_service_account" "app-manager" {
  display_name = "app-${var.topic_name}-manager"
  description  = "Service account to manage ${var.kafka_id} Kafka cluster"
}

resource "confluent_role_binding" "app-manager-kafka-cluster-admin" {
  principal   = "User:${confluent_service_account.app-manager.id}"
  role_name   = "CloudClusterAdmin"
  crn_pattern = data.confluent_kafka_cluster.main.rbac_crn
}

resource "confluent_api_key" "app-manager-kafka-api-key" {
  display_name = "app-manager-kafka-api-key"
  description  = "Kafka API Key that is owned by ${confluent_service_account.app-manager.display_name} service account"
  owner {
    id          = confluent_service_account.app-manager.id
    api_version = confluent_service_account.app-manager.api_version
    kind        = confluent_service_account.app-manager.kind
  }

  managed_resource {
    id          = data.confluent_kafka_cluster.main.id
    api_version = data.confluent_kafka_cluster.main.api_version
    kind        = data.confluent_kafka_cluster.main.kind

    environment {
      id = var.environment_id
    }
  }

  # The goal is to ensure that confluent_role_binding.app-manager-kafka-cluster-admin is created before
  # confluent_api_key.app-manager-kafka-api-key is used to create instances of
  # confluent_kafka_topic, confluent_kafka_acl resources.

  # 'depends_on' meta-argument is specified in confluent_api_key.app-manager-kafka-api-key to avoid having
  # multiple copies of this definition in the configuration which would happen if we specify it in
  # confluent_kafka_topic, confluent_kafka_acl resources instead.
  depends_on = [
    confluent_role_binding.app-manager-kafka-cluster-admin
  ]
}

resource "confluent_service_account" "app-consumer" {
  display_name = "app-${var.topic_name}-consumer"
  description  = "Service account to consume from '${var.topic_name}' topic of ${var.kafka_id} Kafka cluster"
}

resource "confluent_api_key" "app-consumer-kafka-api-key" {
  display_name = "${confluent_service_account.app-consumer.display_name}-kafka-api-key"
  description  = "Kafka API Key that is owned by ${confluent_service_account.app-consumer.display_name} service account"
  owner {
    id          = confluent_service_account.app-consumer.id
    api_version = confluent_service_account.app-consumer.api_version
    kind        = confluent_service_account.app-consumer.kind
  }

  managed_resource {
    id          = data.confluent_kafka_cluster.main.id
    api_version = data.confluent_kafka_cluster.main.api_version
    kind        = data.confluent_kafka_cluster.main.kind

    environment {
      id = var.environment_id
    }
  }
}

resource "confluent_service_account" "app-producer" {
  display_name = "app-${var.topic_name}-producer"
  description  = "Service account to produce to '${var.topic_name}' topic of ${var.kafka_id} Kafka cluster"
}

resource "confluent_api_key" "app-producer-kafka-api-key" {
  display_name = "${confluent_service_account.app-producer.display_name}-kafka-api-key"
  description  = "Kafka API Key that is owned by ${confluent_service_account.app-producer.display_name} service account"
  owner {
    id          = confluent_service_account.app-producer.id
    api_version = confluent_service_account.app-producer.api_version
    kind        = confluent_service_account.app-producer.kind
  }

  managed_resource {
    id          = data.confluent_kafka_cluster.main.id
    api_version = data.confluent_kafka_cluster.main.api_version
    kind        = data.confluent_kafka_cluster.main.kind

    environment {
      id = var.environment_id
    }
  }
}

resource "confluent_kafka_topic" "main" {
  kafka_cluster {
    id = var.kafka_id
  }

  topic_name    = var.topic_name
  rest_endpoint = data.confluent_kafka_cluster.main.rest_endpoint
  credentials {
    key    = confluent_api_key.app-manager-kafka-api-key.id
    secret = confluent_api_key.app-manager-kafka-api-key.secret
  }
}

// Create ACLs
resource "confluent_kafka_acl" "app-producer-write-on-topic" {
  kafka_cluster {
    id = var.kafka_id
  }

  resource_type = "TOPIC"
  resource_name = confluent_kafka_topic.main.topic_name
  pattern_type  = "LITERAL"
  principal     = "User:${confluent_service_account.app-producer.id}"
  host          = "*"
  operation     = "WRITE"
  permission    = "ALLOW"
  rest_endpoint = data.confluent_kafka_cluster.main.rest_endpoint
  credentials {
    key    = confluent_api_key.app-manager-kafka-api-key.id
    secret = confluent_api_key.app-manager-kafka-api-key.secret
  }
}

// Note that in order to consume from a topic, the principal of the consumer ('app-consumer' service account)
// needs to be authorized to perform 'READ' operation on both Topic and Group resources:
// confluent_kafka_acl.app-consumer-read-on-topic, confluent_kafka_acl.app-consumer-read-on-group.
// https://docs.confluent.io/platform/current/kafka/authorization.html#using-acls
resource "confluent_kafka_acl" "app-consumer-read-on-topic" {
  kafka_cluster {
    id = var.kafka_id
  }

  resource_type = "TOPIC"
  resource_name = confluent_kafka_topic.main.topic_name
  pattern_type  = "LITERAL"
  principal     = "User:${confluent_service_account.app-consumer.id}"
  host          = "*"
  operation     = "READ"
  permission    = "ALLOW"
  rest_endpoint = data.confluent_kafka_cluster.main.rest_endpoint
  credentials {
    key    = confluent_api_key.app-manager-kafka-api-key.id
    secret = confluent_api_key.app-manager-kafka-api-key.secret
  }
}

resource "confluent_kafka_acl" "app-consumer-read-on-group" {
  kafka_cluster {
    id = var.kafka_id
  }

  resource_type = "GROUP"
  // The existing values of resource_name, pattern_type attributes are set up to match Confluent CLI's default consumer group ID ("confluent_cli_consumer_<uuid>").
  // https://docs.confluent.io/confluent-cli/current/command-reference/kafka/topic/confluent_kafka_topic_consume.html
  // Update the values of resource_name, pattern_type attributes to match your target consumer group ID.
  // https://docs.confluent.io/platform/current/kafka/authorization.html#prefixed-acls
  resource_name = "confluent_cli_consumer_"
  pattern_type  = "PREFIXED"
  principal     = "User:${confluent_service_account.app-consumer.id}"
  host          = "*"
  operation     = "READ"
  permission    = "ALLOW"
  rest_endpoint = data.confluent_kafka_cluster.main.rest_endpoint
  credentials {
    key    = confluent_api_key.app-manager-kafka-api-key.id
    secret = confluent_api_key.app-manager-kafka-api-key.secret
  }
}
