---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "confluent_flink_connection Resource - terraform-provider-confluent"
subcategory: ""
description: |-
  
---

# confluent_flink_connection Resource

[![General Availability](https://img.shields.io/badge/Lifecycle%20Stage-General%20Availability-%2345c6e8)](https://docs.confluent.io/cloud/current/api.html#section/Versioning/API-Lifecycle-Policy)

`confluent_flink_connection` provides a Flink Connection resource that enables creating, editing, and deleting Flink Connection on Confluent Cloud.

-> **Note:** It is recommended to set `lifecycle { prevent_destroy = true }` on production instances to prevent accidental Flink Connection deletion. This setting rejects plans that would destroy or recreate the Flink Connection, such as attempting to change uneditable attributes. Read more about it in the [Terraform docs](https://www.terraform.io/language/meta-arguments/lifecycle#prevent_destroy).

## Example Usage
### Option #1: Manage multiple Flink Compute Pools in the same Terraform workspace

```terraform
provider "confluent" {
  cloud_api_key    = var.confluent_cloud_api_key    # optionally use CONFLUENT_CLOUD_API_KEY env var
  cloud_api_secret = var.confluent_cloud_api_secret # optionally use CONFLUENT_CLOUD_API_SECRET env var
}

resource "confluent_flink_connection" "connection1" {
  organization {
    id = data.confluent_organization.main.id
  }
  environment {
    id = data.confluent_environment.staging.id
  }
  compute_pool {
    id = confluent_flink_compute_pool.example.id
  }
  principal {
    id = confluent_service_account.app-manager-flink.id
  }
  rest_endpoint = data.confluent_flink_region.main.rest_endpoint
  credentials {
    key    = confluent_api_key.env-admin-flink-api-key.id
    secret = confluent_api_key.env-admin-flink-api-key.secret
  }
  
  display_name = "connection1"
  type = "OPENAI"
  endpoint = "https://api.openai.com/v1/chat/completions"
  api_key ="API_Key_value"
  
  lifecycle {
    prevent_destroy = true
  }
}
```

### Option #2: Manage a single Flink Compute Pool in the same Terraform workspace

```terraform
provider "confluent" {
  organization_id       = var.organization_id            # optionally use CONFLUENT_ORGANIZATION_ID env var
  environment_id        = var.environment_id             # optionally use CONFLUENT_ENVIRONMENT_ID env var
  flink_compute_pool_id = var.flink_compute_pool_id      # optionally use FLINK_COMPUTE_POOL_ID env var
  flink_rest_endpoint   = var.flink_rest_endpoint        # optionally use FLINK_REST_ENDPOINT env var
  flink_api_key         = var.flink_api_key              # optionally use FLINK_API_KEY env var
  flink_api_secret      = var.flink_api_secret           # optionally use FLINK_API_SECRET env var
  flink_principal_id    = var.flink_principal_id         # optionally use FLINK_PRINCIPAL_ID env var
}

resource "confluent_flink_connection" "example" {
  display_name = "connection1"
  type = "OPENAI"
  endpoint = "https://api.openai.com/v1/chat/completions"
  api_key ="API_Key_value"

  lifecycle {
    prevent_destroy = true
  }
}
```

The following arguments are supported:

- `organization` (Optional Configuration Block) supports the following:
    - `id` - (Required String) The ID of the Organization, for example, `1111aaaa-11aa-11aa-11aa-111111aaaaaa`.
- `environment` (Optional Configuration Block) supports the following:
    - `id` - (Required String) The ID of the Environment, for example, `env-abc123`.
- `compute_pool` - (Optional Configuration Block) supports the following:
    - `id` - (Required String) The ID of the Flink Compute Pool, for example, `lfcp-abc123`.
- `principal` - (Optional Configuration Block) supports the following:
    - `id` - (Required String) The ID of the Principal the Flink Connection runs as, for example, `sa-abc123`.
- `rest_endpoint` - (Optional String) The REST endpoint of the Flink region, for example, `https://flink.us-east-1.aws.confluent.cloud`.
- `credentials` (Optional Configuration Block) supports the following:
    - `key` - (Required String) The Flink API Key.
    - `secret` - (Required String, Sensitive) The Flink API Secret.

-> **Note:** A Flink API key consists of a key and a secret. Flink API keys are required to interact with Flink Connections in Confluent Cloud. Each Flink API key is valid for one specific Flink Region.

-> **Note:** Use Option #2 to simplify the key rotation process. When using Option #1, to rotate a Flink API key, create a new Flink API key, update the `credentials` block in all configuration files to use the new Flink API key, run `terraform apply -target="confluent_flink_connection.example"`, and remove the old Flink API key. Alternatively, in case the old Flink API Key was deleted already, you might need to run `terraform plan -refresh=false -target="confluent_flink_connection.example" -out=rotate-flink-api-key` and `terraform apply rotate-flink-api-key` instead.

- `display_name` - (Required String) The name of the Flink Connection.
- `type` - (Required String) The type of the Flink Connection. The accepted values are: `OPENAI`, `AZUREML`, `AZUREOPENAI`, `BEDROCK`, `SAGEMAKER`, `GOOGLEAI`, `VERTEXAI`, `MONGODB`, `PINECONE`, `ELASTIC` and `COUCHBASE`.
- `endpoint` - (Required String) The endpoint of the Flink Connection, for example, `https://api.openai.com/v1/chat/completions`
- `api_key` - (Optional String) The api key for the connection type. This is valid and required for types `OPENAI`, `AZUREML`, `AZUREOPENAI`, `GOOGLEAI`, `ELASTIC` and `PINECONE`.
- `aws_access_key` - (Optional String) The AWS access key for the connection type. This is valid and required for types `BEDROCK` and `SAGEMAKER`.
- `aws_secret_key` - (Optional String) The AWS secret key for the connection type. This is valid and required for types `BEDROCK` and `SAGEMAKER`.
- `aws_session_token` - (Optional String) The AWS session key for the connection type. This is valid and required for types `BEDROCK` and `SAGEMAKER`.
- `service_key` - (Optional String) The service key for the connection type. This is valid and required for type `VERTEXAI`.
- `username` - (Optional String) The username for the connection type. This is valid and required for types `MONGODB` and `COUCHBASE`.
- `password` - (Optional String) The password  for the connection type. This is valid and required for types `MONGODB` and `COUCHBASE`.

!> **Warning:** Use Option #2 to avoid exposing sensitive `credentials` value in a state file. When using Option #1, Terraform doesn't encrypt the sensitive `credentials` value of the `confluent_flink_connection` resource, so you must keep your state file secure to avoid exposing it. Refer to the [Terraform documentation](https://www.terraform.io/docs/language/state/sensitive-data.html) to learn more about securing your state file.

# Attributes Reference

In addition to the preceding arguments, the following attributes are exported:

- `id` - (Required String) The ID of the Flink connection, in the format `<Organization ID>/<Environment ID>/<Flink Connection name>`, for example, `org-xyz123/env-abc123/connection1`.
- `api_version` - (Required String) The API Version of the schema version of the Flink Connection, for example, `sql/v1`.
- `kind` - (Required String) The kind of the Flink Connection, for example, `Connection`.

## Import

You can import a Flink connection by using the Flink Connection name, for example:

```shell
# Option #1: Manage multiple Flink Compute Pools in the same Terraform workspace
$ export IMPORT_CONFLUENT_ORGANIZATION_ID="<organization_id>"
$ export IMPORT_CONFLUENT_ENVIRONMENT_ID="<environment_id>"
$ export IMPORT_FLINK_COMPUTE_POOL_ID="<flink_compute_pool_id>"
$ export IMPORT_FLINK_API_KEY="<flink_api_key>"
$ export IMPORT_FLINK_API_SECRET="<flink_api_secret>"
$ export IMPORT_FLINK_REST_ENDPOINT="<flink_rest_endpoint>"
$ export IMPORT_FLINK_PRINCIPAL_ID="<flink_principal>"
$ export API_KEY="<API_KEY>"
$ terraform import confluent_flink_connection.example org-xyz123/env-abc123/connection1

# Option #2: Manage a single Flink Compute Pool in the same Terraform workspace
$ export API_KEY="<API_KEY>"
$ terraform import confluent_flink_connection.example org-xyz123/env-abc123/connection1
```

-> **Note:** The example above is for `type = OPENAI`, so we exported `API_KEY`. Export the required field for each type as mentioned above. The fields that can be exported are: `API_KEY`, `AWS_SECRET_ACCESS_KEY_CONNECTION`, `AWS_ACCESS_KEY_ID_CONNECTION`, `AWS_SESSION_TOKEN_CONNECTION`, `SERVICE_KEY`, `USERNAME`, `PASSWORD`. 

!> **Warning:** Do not forget to delete terminal command history afterwards for security purposes.

