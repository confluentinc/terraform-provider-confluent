---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "confluent_role_binding Resource - terraform-provider-confluent"
subcategory: ""
description: |-
  
---

# confluent_role_binding Resource

[![General Availability](https://img.shields.io/badge/Lifecycle%20Stage-General%20Availability-%2345c6e8)](https://docs.confluent.io/cloud/current/api.html#section/Versioning/API-Lifecycle-Policy)

`confluent_role_binding` provides a Role Binding resource that enables creating, reading, and deleting role bindings on Confluent Cloud.

-> **Note:** For more information on the Role Bindings, see [Predefined RBAC roles in Confluent Cloud](https://docs.confluent.io/cloud/current/access-management/access-control/rbac/predefined-rbac-roles.html).

## Example Usage

```terraform
resource "confluent_role_binding" "org-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "MetricsViewer"
  crn_pattern = data.confluent_organization.demo.resource_name
}

resource "confluent_role_binding" "environment-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "EnvironmentAdmin"
  crn_pattern = confluent_environment.stag.resource_name
}

resource "confluent_role_binding" "environment-example-rb-skip-sync" {
  principal              = "User:${confluent_service_account.test.id}"
  role_name              = "EnvironmentAdmin"
  crn_pattern            = confluent_environment.stag.resource_name
  disable_wait_for_ready = true
}

resource "confluent_role_binding" "environment-example-rb-2" {
  principal   = "User:${confluent_identity_pool.test.id}"
  role_name   = "EnvironmentAdmin"
  crn_pattern = confluent_environment.stag.resource_name
}

resource "confluent_role_binding" "data-discovery-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "DataDiscovery"
  crn_pattern = confluent_environment.stag.resource_name
}

resource "confluent_role_binding" "network-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "NetworkAdmin"
  crn_pattern = data.confluent_organization.demo.resource_name
}

resource "confluent_role_binding" "cluster-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "CloudClusterAdmin"
  crn_pattern = confluent_kafka_cluster.basic.rbac_crn
}

resource "confluent_role_binding" "topic-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "DeveloperWrite"
  crn_pattern = "${confluent_kafka_cluster.standard.rbac_crn}/kafka=${confluent_kafka_cluster.standard.id}/topic=${confluent_kafka_topic.orders.topic_name}"
}

resource "confluent_role_binding" "topic-example-rb-2" {
  principal   = "User:${confluent_identity_pool.test.id}"
  role_name   = "DeveloperWrite"
  crn_pattern = "${confluent_kafka_cluster.standard.rbac_crn}/kafka=${confluent_kafka_cluster.standard.id}/topic=${confluent_kafka_topic.orders.topic_name}"
}

resource "confluent_role_binding" "group-example-rb" {
  principal = "User:${confluent_service_account.test.id}"
  role_name = "DeveloperRead"
  // The existing value of crn_pattern's suffix (group=confluent_cli_consumer_*) are set up to match Confluent CLI's default consumer group ID ("confluent_cli_consumer_<uuid>").
  // https://docs.confluent.io/confluent-cli/current/command-reference/kafka/topic/confluent_kafka_topic_consume.html
  // Update it to match your target consumer group ID.
  crn_pattern = "${confluent_kafka_cluster.basic.rbac_crn}/kafka=${confluent_kafka_cluster.standard.id}/group=confluent_cli_consumer_*"
}

resource "confluent_role_binding" "group-mapping-example-rb" {
  principal   = "User:${confluent_group_mapping.application-developers.id}"
  role_name   = "EnvironmentAdmin"
  crn_pattern = confluent_environment.stag.resource_name
}

resource "confluent_role_binding" "transaction-example-rb" {
  principal = "User:${confluent_service_account.test.id}"
  role_name = "DeveloperRead"
  crn_pattern = "${confluent_kafka_cluster.basic.rbac_crn}/kafka=${confluent_kafka_cluster.standard.id}/transactional-id=my_transaction"
}

resource "confluent_role_binding" "connector-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "DeveloperRead"
  crn_pattern = "${confluent_kafka_cluster.standard.rbac_crn}/connector=${local.connector_name}"
}

resource "confluent_role_binding" "all-subjects-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "DeveloperRead"
  crn_pattern = "${data.confluent_schema_registry_cluster.example.resource_name}/subject=*"
}

resource "confluent_role_binding" "subject-foo-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "DeveloperRead"
  crn_pattern = "${data.confluent_schema_registry_cluster.example.resource_name}/subject=foo"
}

resource "confluent_role_binding" "subject-with-abc-prefix-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "DeveloperRead"
  crn_pattern = "${data.confluent_schema_registry_cluster.example.resource_name}/subject=abc*"
}

resource "confluent_role_binding" "kek-example-rb" {
  principal   = "User:${confluent_service_account.test.id}"
  role_name   = "DeveloperRead"
  crn_pattern = "${data.confluent_schema_registry_cluster.example.resource_name}/kek=kek-name"
}

locals {
  connector_name = lookup(confluent_connector.test.config_nonsensitive, "name", "\"name\" attribute is missing")
}
```

<!-- schema generated by tfplugindocs -->
## Argument Reference

The following arguments are supported:

- `principal` - (Required String) A principal User to bind the role to, for example, "User:u-111aaa" for binding to a user "u-111aaa", or "User:sa-111aaa" for binding to a service account "sa-111aaa".
- `role_name` - (Required String) A name of the role to bind to the principal. See [Confluent Cloud RBAC Roles](https://docs.confluent.io/cloud/current/access-management/access-control/cloud-rbac.html#ccloud-rbac-roles) for a full list of supported role names.
- `crn_pattern` - (Required String) A [Confluent Resource Name (CRN)](https://docs.confluent.io/cloud/current/api.html#section/Identifiers-and-URLs/Confluent-Resource-Names-(CRNs)) that specifies the scope and resource patterns necessary for the role to bind.
- `disable_wait_for_ready` - (Optional Boolean) An optional flag to disable wait-for-readiness on create. Its primary use case is to ensure that the role bindings are properly synchronized across the system before exiting the Terraform creation step. Must be unset when importing. Defaults to `false`.

!> **Warning:** When `disable_wait_for_ready = true` is used, Terraform skips waiting for role bindings to fully propagate. This can lead to a situation where Terraform attempts to create resources before the service account has the necessary permissions—resulting in HTTP 403 Forbidden errors.
For example, if you're creating a new service account, a new Kafka API Key, a new CloudClusterAdmin role binding, and a Kafka topic in a single run (see this [code snippet](https://github.com/confluentinc/terraform-provider-confluent/blob/master/examples/configurations/standard-kafka-rbac/main.tf#L46-L100)), the topic creation may fail if the role binding hasn’t taken effect yet. Without that role, the service account won’t have permission to create the topic.
This setting is best suited for scenarios where you're provisioning a large number of role bindings without dependent resources, as it significantly speeds up the apply process.

-> **Note:** If you encounter HTTP 403 Forbidden errors when creating role bindings, feel free to rerun `terraform apply` after a few minutes, once the role bindings have had time to propagate.

-> **Note:** Feel free to use `time_sleep` [resource](https://registry.terraform.io/providers/hashicorp/time/latest/docs/resources/sleep) to configure a custom waiting period, see [this example](#example-of-using-time_sleep) for more details.

## Attributes Reference

In addition to the preceding arguments, the following attributes are exported:

- `id` - (Required String) The ID of the Role Binding (e.g., `rb-f3a90de`).

## Import

-> **Note:** `CONFLUENT_CLOUD_API_KEY` and `CONFLUENT_CLOUD_API_SECRET` environment variables must be set before importing a Role Binding.

You can import a Role Binding by using Role Binding ID, for example:

```shell
$ export CONFLUENT_CLOUD_API_KEY="<cloud_api_key>"
$ export CONFLUENT_CLOUD_API_SECRET="<cloud_api_secret>"
$ terraform import confluent_role_binding.my_rb rb-f3a90de
```

!> **Warning:** Do not forget to delete terminal command history afterwards for security purposes.

## Getting Started
The following end-to-end examples might help to get started with `confluent_role_binding` resource:
  * [`standard-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/standard-kafka-rbac): _Standard_ Kafka cluster with authorization using RBAC
  * [`dedicated-public-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-public-kafka-rbac): _Dedicated_ Kafka cluster that is accessible over the public internet with authorization using RBAC
  * [`dedicated-privatelink-aws-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-privatelink-aws-kafka-rbac): _Dedicated_ Kafka cluster on AWS that is accessible via PrivateLink connections with authorization using RBAC
  * [`dedicated-privatelink-azure-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-privatelink-azure-kafka-rbac): _Dedicated_ Kafka cluster on Azure that is accessible via PrivateLink connections with authorization using RBAC
  * [`dedicated-vnet-peering-azure-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-vnet-peering-azure-kafka-rbac): _Dedicated_ Kafka cluster on Azure that is accessible via VPC Peering connections with authorization using RBAC
  * [`dedicated-vpc-peering-aws-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-vpc-peering-aws-kafka-rbac): _Dedicated_ Kafka cluster on AWS that is accessible via VPC Peering connections with authorization using RBAC
  * [`dedicated-vpc-peering-gcp-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-vpc-peering-gcp-kafka-rbac): _Dedicated_ Kafka cluster on GCP that is accessible via VPC Peering connections with authorization using RBAC
  * [`dedicated-transit-gateway-attachment-aws-kafka-acls`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-transit-gateway-attachment-aws-kafka-acls): _Dedicated_ Kafka cluster on AWS that is accessible via Transit Gateway Endpoint with authorization using ACLs
  * [`dedicated-transit-gateway-attachment-aws-kafka-rbac`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/dedicated-transit-gateway-attachment-aws-kafka-rbac): _Dedicated_ Kafka cluster on AWS that is accessible via Transit Gateway Endpoint with authorization using RBAC
  * [`enterprise-privatelinkattachment-aws-kafka-acls`](https://github.com/confluentinc/terraform-provider-confluent/tree/master/examples/configurations/enterprise-privatelinkattachment-aws-kafka-acls): _Enterprise_ Kafka cluster on AWS that is accessible via PrivateLink connections with authorization using ACLs

## Example of using time_sleep

This configuration introduces a 10-second custom delay after the creation of a role binding, before creating a Kafka topic.

```terraform
resource "confluent_role_binding" "app-manager-kafka-cluster-admin-skip-sync" {
  principal   = "User:${confluent_service_account.app-manager.id}"
  role_name   = "CloudClusterAdmin"
  crn_pattern = confluent_kafka_cluster.standard.rbac_crn
  disable_wait_for_ready = true
}

resource "time_sleep" "wait_10_seconds_after_role_binding" {
  depends_on = [confluent_role_binding.app-manager-kafka-cluster-admin-skip-sync]
  create_duration = "10s"
}

resource "confluent_kafka_topic" "orders" {
  depends_on = [time_sleep.wait_10_seconds_after_role_binding]
  kafka_cluster {
    id = confluent_kafka_cluster.standard.id
  }
  topic_name    = "orders"
  rest_endpoint = confluent_kafka_cluster.standard.rest_endpoint
  credentials {
    key    = confluent_api_key.app-manager-kafka-api-key.id
    secret = confluent_api_key.app-manager-kafka-api-key.secret
  }
}
```