---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "confluent_catalog_entity_attributes Resource - terraform-provider-confluent"
subcategory: ""
description: |-
  
---

# confluent_catalog_entity_attributes Resource

[![Preview](https://img.shields.io/badge/Lifecycle%20Stage-Preview-%2300afba)](https://docs.confluent.io/cloud/current/api.html#section/Versioning/API-Lifecycle-Policy)

-> **Note:** `confluent_catalog_entity_attributes` resource is available in **Preview** for early adopters. Preview features are introduced to gather customer feedback. This feature should be used only for evaluation and non-production testing purposes or to provide feedback to Confluent, particularly as it becomes more widely available in follow-on editions.  
**Preview** features are intended for evaluation use in development and testing environments only, and not for production use. The warranty, SLA, and Support Services provisions of your agreement with Confluent do not apply to Preview features. Preview features are considered to be a Proof of Concept as defined in the Confluent Cloud Terms of Service. Confluent may discontinue providing preview releases of the Preview features at any time in Confluentâ€™s sole discretion.

## Example Usage

### Option #1: Manage multiple Schema Registry clusters in the same Terraform workspace

```terraform
provider "confluent" {
  cloud_api_key    = var.confluent_cloud_api_key    # optionally use CONFLUENT_CLOUD_API_KEY env var
  cloud_api_secret = var.confluent_cloud_api_secret # optionally use CONFLUENT_CLOUD_API_SECRET env var
}

resource "confluent_catalog_entity_attributes" "topic" {
  schema_registry_cluster {
    id = data.confluent_schema_registry_cluster.essentials.id
  }
  rest_endpoint = data.confluent_schema_registry_cluster.essentials.rest_endpoint
  credentials {
    key    = "<Schema Registry API Key for data.confluent_schema_registry_cluster.essentials>"
    secret = "<Schema Registry API Secret for data.confluent_schema_registry_cluster.essentials>"
  }
  
  # example: entity_name = "lkc-15xq83:topic_0"
  entity_name = "${data.confluent_kafka_cluster.basic.id}:${confluent_kafka_topic.purchase.topic_name}"
  entity_type = "kafka_topic"
  attributes = {
    "owner"       = "dev"
    "description" = "Kafka topic for orders"
    "ownerEmail"  = "dev@gmail.com"
  }

  lifecycle {
    prevent_destroy = true
  }
}

resource "confluent_catalog_entity_attributes" "schema" {
  schema_registry_cluster {
    id = data.confluent_schema_registry_cluster.essentials.id
  }
  rest_endpoint = data.confluent_schema_registry_cluster.essentials.rest_endpoint
  credentials {
    key    = "<Schema Registry API Key for data.confluent_schema_registry_cluster.essentials>"
    secret = "<Schema Registry API Secret for data.confluent_schema_registry_cluster.essentials>"
  }

  # example: entity_name = "lsrc-1w9075:.:100008"
  entity_name = "${data.confluent_schema_registry_cluster.essentials.id}:.:${confluent_schema.purchase.schema_identifier}"
  entity_type = "sr_schema"
  attributes = {
    "description" = "Schema description"
  }

  lifecycle {
    prevent_destroy = true
  }
}

resource "confluent_catalog_entity_attributes" "kafka-cluster" {
  schema_registry_cluster {
    id = data.confluent_schema_registry_cluster.essentials.id
  }
  rest_endpoint = data.confluent_schema_registry_cluster.essentials.rest_endpoint
  credentials {
    key    = "<Schema Registry API Key for data.confluent_schema_registry_cluster.essentials>"
    secret = "<Schema Registry API Secret for data.confluent_schema_registry_cluster.essentials>"
  }

  # example: entity_name = "lkc-15xq83"
  entity_name = data.confluent_kafka_cluster.basic.id
  entity_type = "kafka_logical_cluster"
  attributes = {
    "description" = "Kafka Cluster description"
  }

  lifecycle {
    prevent_destroy = true
  }
}
```

### Option #2: Manage a single Schema Registry cluster in the same Terraform workspace

```terraform
provider "confluent" {
  schema_registry_id            = var.schema_registry_id            # optionally use SCHEMA_REGISTRY_ID env var
  schema_registry_rest_endpoint = var.schema_registry_rest_endpoint # optionally use SCHEMA_REGISTRY_REST_ENDPOINT env var
  schema_registry_api_key       = var.schema_registry_api_key       # optionally use SCHEMA_REGISTRY_API_KEY env var
  schema_registry_api_secret    = var.schema_registry_api_secret    # optionally use SCHEMA_REGISTRY_API_SECRET env var
}

resource "confluent_catalog_entity_attributes" "topic" {
  # example: entity_name = "lkc-15xq83:topic_0"
  entity_name = "${var.kafka_cluster_id}:${var.kafka_topic_name}"
  entity_type = "kafka_topic"
  attributes = {
    "owner"       = "dev"
    "description" = "Kafka topic for orders"
    "ownerEmail"  = "dev@gmail.com"
  }

  lifecycle {
    prevent_destroy = true
  }
}

resource "confluent_catalog_entity_attributes" "schema" {
  # example: entity_name = "lsrc-1w9075:.:100008"
  entity_name = "${var.schema_registry_cluster_id}:.:${confluent_schema.purchase.schema_identifier}"
  entity_type = "sr_schema"
  attributes = {
    "description" = "Schema description"
  }

  lifecycle {
    prevent_destroy = true
  }
}

resource "confluent_catalog_entity_attributes" "kafka-cluster" {
  # example: entity_name = "lkc-15xq83"
  entity_name = var.kafka_cluster_id
  entity_type = "kafka_logical_cluster"
  attributes = {
    "description" = "Kafka Cluster description"
  }

  lifecycle {
    prevent_destroy = true
  }
}
```

<!-- schema generated by tfplugindocs -->
## Argument Reference

The following arguments are supported:

- `schema_registry_cluster` - (Optional Configuration Block) supports the following:
    - `id` - (Required String) The ID of the Schema Registry cluster, for example, `lsrc-abc123`.
- `rest_endpoint` - (Optional String) The REST endpoint of the Schema Registry cluster, for example, `https://psrc-00000.us-central1.gcp.confluent.cloud:443`).
- `credentials` (Optional Configuration Block) supports the following:
    - `key` - (Required String) The Schema Registry API Key.
    - `secret` - (Required String, Sensitive) The Schema Registry API Secret.
- `entity_name` - (Required String) The qualified name of the entity, for example, `${data.confluent_schema_registry_cluster.essentials.id}:.:${confluent_schema.purchase.schema_identifier}`, `${data.confluent_schema_registry_cluster.essentials.id}:${confluent_kafka_cluster.basic.id}:${confluent_kafka_topic.purchase.topic_name}`.
- `entity_type` - (Required String) The entity type, for example, `sr_schema`, `kafka_topic`, `kafka_logical_cluster`, etc. Refer to the [Entity types](https://docs.confluent.io/cloud/current/stream-governance/stream-catalog-rest-apis.html#entity-types) to learn more about entity types.
- `attributes` - (Optional Map) The block of key-value pair attributes. Refer to the [add-a-topic-owner-and-email](https://docs.confluent.io/cloud/current/stream-governance/stream-catalog-rest-apis.html#add-a-topic-owner-and-email) for more details.

-> **Note:** You have to set the attribute value to an empty string if you plan to delete an attribute.

-> **Note:** A Schema Registry API key consists of a key and a secret. Schema Registry API keys are required to interact with Schema Registry clusters in Confluent Cloud. Each Schema Registry API key is valid for one specific Schema Registry cluster.

-> **Note:** Use Option #2 to simplify the key rotation process. When using Option #1, to rotate a Schema Registry API key, create a new Schema Registry API key, update the `credentials` block in all configuration files to use the new Schema Registry API key, run `terraform apply -target="confluent_catalog_entity_attributes.main"`, and remove the old Schema Registry API key. Alternatively, in case the old Schema Registry API Key was deleted already, you might need to run `terraform plan -refresh=false -target="confluent_catalog_entity_attributes.main" -out=rotate-schema-registry-api-key` and `terraform apply rotate-schema-registry-api-key` instead.

!> **Warning:** Use Option #2 to avoid exposing sensitive `credentials` value in a state file. When using Option #1, Terraform doesn't encrypt the sensitive `credentials` value of the `confluent_catalog_entity_attributes` resource, so you must keep your state file secure to avoid exposing it. Refer to the [Terraform documentation](https://www.terraform.io/docs/language/state/sensitive-data.html) to learn more about securing your state file.

## Attributes Reference

In addition to the preceding arguments, the following attributes are exported:

- `id` - (Required String) The ID of the Entity Attributes, in the format `<Entity Type>/<Entity Name>`, for example, `lsrc-8wrx70/PII/lsrc-8wrx70:.:100001/sr_schema`.
